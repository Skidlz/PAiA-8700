                        ;*********************************
                        ;*                               *
                        ;*         PINK TUNES            *
                        ;*                               *
                        ;*     A COMPOSING PROGRAM       *
                        ;*    FOR FOUR PART HARMONIES    *
                        ;*                               *
                        ;*     BY JOHN S SIMONTON, JR    *
                        ;*(C) 1978 PAIA ELECTRONICS, INC *
                        ;*********************************
                        ;
0810 =                  KBD		= $0810
0820 =                  DISP	= $0820
0d21 =                  INIT	= $0D21
0d2b =                  NOTE	= $0D2B
0f00 =                  DECD	= $0F00
ffc0 =                  BRAK	= $FFC0	;BREAK ROUTINE IN PIEBUG
                        
0087 =                  TIME	= $0087
008b =                  MASK	= $008B
00bf =                  CLCK	= $00BF
00bf =                  NTB7	= $00BF
00c3 =                  NTBB	= $00C3
00cf =                  NTMP	= $00CF
00db =                  NT0B	= $00db
00df =                  KTBL	= $00DF
00ea =                  OUTS	= $00EA
00eb =                  OUTT	= $00EB
00ec =                  TEMP	= $00EC	;TEMPORARY STORAGE
                        
0000 =                  	org	$0000
                        ;
                        ;FIRST ATTEND TO HOUSKEEPING--
                        ;
0000 : 4cc0ff           BEG		jmp BRAK		;BREAK VECTOR
0003 : 20210d           STAR    jsr INIT		;SETUP SYNTH - MUS1
0006 : ad1008                   lda KBD			;INITIALIZE RANDOM
0009 : 85d0                     sta <NTMP+01	;NUMBER GENERATOR
000b : 207101           LOOP    jsr SET			;INIT PINK TUNES
000e : 202b0d           LP0     jsr NOTE		;PLAY NOTE, READ AGO
                        ;
                        ;CHECK FOR ADDITION TO CANDIDATE
                        ;NOTE TABLE
                        ;
0011 : a5e7             MAIN    lda <KTBL+08	;ANY KEYS DOWN?
0013 : f002                     beq OUT1		;NO-CHECK FOR TIME OUT
0015 : c5ec                     cmp <TEMP		;YES-A NEW KEY?
0017 : 85ec             OUT1    sta <TEMP		;SAVE FOR NEXT TIME
0019 : f00a                     beq OUT			;BRANCH IS SAME KEY
                        ;
001b : a210                     ldx #$10		;IF NEW KEY SHIFT
001d : b48f             LP3     ldy <NBUF-1,x	;ALL 16 CANDIDATES
001f : 958f                     sta <NBUF-1,x	;DOWN BY ONE
0021 : 98                       tya
0022 : ca                       dex
0023 : d0f8                     bne LP3			;NOT DONE-LOOP
                        ;
                        ;NOW CHECK FOR CLOCK TIME OUT
                        ;
0025 : a5bf             OUT     lda <CLCK		;GET MASTER CLOCK
0027 : d01a                     bne TEST		;AND IF TIMES OUT
0029 : a5a9                     lda <TMPO		;SET TO TEMPO VALUE
002b : 85bf                     sta <CLCK		;CALL SUB FOR NEW
002d : 205301                   jsr ALOC		;NOTES (IF NEEDED)
0030 : a5a8                     lda <LNTH		;GET CYCLE STATUS
0032 : 8d2008                   sta DISP		;SHOW IT AND IF ZERO
0035 : f00c                     beq TEST		;CYCLE IS COMPLETE
0037 : c6a8                     dec <LNTH		;IF NOT DONE, DCRMNT
0039 : d008                     bne TEST		;IF NOT ZERO NOWLEAVE
003b : 207101                   jsr SET			;IF ZERO, REINIT
003e : 205301                   jsr ALOC		;GET FIRST NOTES AND
0041 : f0cb                     beq LP0			;BRANCH ALWAYS TO PLAY
0043 : 20000f           TEST    jsr DECD		;GET A COMMAND - MUS-1
0046 : d00c                     bne TST2		;NOT ZERO, NEXT TEST
0048 : a203                     ldx #$03		;COMMAND 0, NEW TUNE
                                                ;SET POINTER/COUNTER
004a : 200001           TST1    jsr RNDM		;GET RANDOM NUMBER
004d : 95cf                     sta <NTMP,x		;NEW INITIAL RANDOM
004f : ca                       dex				;POINT TO NEXT
0050 : d0f8                     bne TST1		;NOT DONE - LOOP
0052 : f0b7                     beq LOOP		;BRANCH ALWAYS
0054 : c901             TST2    cmp #$01		;COMMAND 1, TUNEING
0056 : d00c                     bne TST4		;NOT 1, TEST NEXT
0058 : a204                     ldx #$04		;4 OUTPUT BUFFERS
005a : a95c                     lda #$5C		;PUT MIDDLE C IN ALL
005c :                  TST3    ;sta NT0B,x  	;OUTPUT BUFFERS
005c : 9ddb00           		db $9D, $DB, $00 ;force addressing mode
005f : ca                       dex
0060 : d0fa                     bne TST3		;NOT DONE-LOOP
0062 : f0aa                     beq LP0			;BRANCH ALWAYS
0064 : c902             TST4    cmp #$02		;COMMAND 2, STOP
0066 : d0a6                     bne LP0			;NO COMMAND - LOOP
0068 : 207101                   jsr SET			;CALL TO ZERO OUT-BUFFS
006b : 202b0d                   jsr NOTE		;THEN MUTE SYNTHESIZER
006e : 00                       brk				;AND RETURN TO PIEBUG
                        
0088 =                          org     $0088
0088 : 02               TIMED   db      $02		;$0088
0089 : 04               TIMEC   db      $04
008a : 01               TIMEB   db      $01
008b : 01               TIMEA   db      $01
                        
008c : f2               MASKD   db      $F2		;$008C
008d : f0               MASKC   db      $F0
008e : f3               MASKB   db      $F3
008f : f3               MASKA   db      $F3
                        
0090 : 5a               NBUF	db      $5A		;$0090 - CANDIDATE ARRAY
0091 : 5d               		db      $5D
0092 : 5f                       db      $5F
0093 : 62                       db      $62
0094 : 64                       db      $64
0095 : 62                       db      $62
0096 : 5f                       db      $5F
0097 : 5d                       db      $5D
0098 : 5a                       db      $5A
0099 : 58                       db      $58
009a : 56                       db      $56
009b : 53                       db      $53
009c : 51               		db		$51
009d : 53               		db		$53
009e : 56                       db      $56
009f : 58                       db      $58
                        		
00a0 : 00               RND0    db      $00		;$00A0 - 5 DICE/RANDOM NUMBERS
00a1 : 00                       db      $00		;$00A1
00a2 : 00                       db      $00		;$00A2
00a3 : 00                       db      $00		;$00A3
00a4 : 00               NOIS    db      $00		;$00A4
                        
00a5 : 00               		db      $00		;$00A5
00a6 : 00                       db      $00
00a7 : 00                       db      $00
                        
00a8 : 00               LNTH    db      $00		;$00A8 - CYCLE LENGTH
                        
00a9 : fa               TMPO    db      $FA
                        
00e8 =                          org     $00E8
00e8 : 00               CTRL	db		$00		;MUS-1 VARIABLES
00e9 : 20               ODLY	db		$20
                        
0100 =                          org     $0100
                        ;
                        ;      RANDOM NUMBER GENERATOR
                        ;
                        ;ESSENTIALLY A 22 BIT LONG SHIFT
                        ;REGISTER WITH EX-0R TAPS AT
                        ;STAGES 22 AND 21 FED BACK TO
                        ;INPUT.
                        ;
0100 : 8a               RNDM    txa				;SAVE X
0101 : 48                       pha
0102 : a5a5                     lda <NOIS+1		;LAST BYTE S/R
0104 : 0a                       asl a			;ALIGN BITS 22 &
0105 : 45a5                     eor <NOIS+1		;21 AND DO EX-OR
0107 : 0a                       asl a			;THEN SHIFT RE-
0108 : 0a                       asl a			;SULT TO CARRY
0109 : 0a                       asl a
010a : a203                     ldx #$03		;SET UP PNT/CNT
010c : 36a4             LP1     rol <NOIS,x		;AND SHIFT 3 BYTE
010e : ca                       dex				;SHIFT REGISTER
010f : d0fb                     bne LP1			;BY ONE BIT LEFT
0111 : 68                       pla				;WHEN DONE RE-
0112 : aa                       tax				;STORE X REG.
0113 : a5a7                     lda <NOIS+03	;AND LEAVE WITH
0115 : 60                       rts				;WITH NO. IN ACC.
                        ;
                        ;      NEW NOTE
                        ;
                        ;TAKES CARE OF PICKING PINK NOTE
                        ;FROM CANDIDATE NOTE TABLE AND
                        ;CALCULATES AND UPDATES NOTE TIMERS
                        ;NOTE THAT Y POINTS TO CHANNEL FOR
                        ;UPDATE
                        ;
0116 : a205             NWNT    ldx #$05		;SET UP PNT/CNT
0118 : a5ea                     lda <OUTS		;GET COPY PINKING
011a : c6ea                     dec <OUTS		;COUNTER,DEC ORIGINAL
011c : 45ea                     eor <OUTS		;PATTERN OF CHANGED
011e : 85eb                     sta <OUTT		;BITS - SAVE CHANGES
0120 : a900                     lda #$00		;PREPARE TO SUM DICE
0122 : 46eb             NW1     lsr <OUTT		;CHECK FOR CHANGED
0124 : 900a                     bcc NW2			;BIT - IF CHANGED
0126 : 48                       pha				;SAVE CURRENT TOTAL
0127 : 200001                   jsr RNDM		;GET RANDOM NUBMER
012a : 2903                     and #$03		;MAKE RANGE FROM 0 T0 3
012c : 959f                     sta <RND0-1,x	;SAVE VALUE FOR NEXT
012e : 68                       pla				;RECOVER TOTAL
012f : 18                       clc				;PREPARE ADDITION
0130 : 759f             NW2     adc <RND0-1,x	;ADD VALUE OF DIE
0132 : ca                       dex				;POINT TO NEXT
0133 : d0ed                     bne NW1			;LOOP IF NOT DONE
0135 : aa                       tax				;USE TOTAL AS POINTER
0136 : b590                     lda <NBUF,x		;GET CANDIDATE
0138 : f003                     beq DURA		;ZERO, DO NOTE CHANGE
013a : 99bf00                   sta NTB7,y		;PLACE IN TEMP BUFFER
013d : a5a5             DURA    lda <NOIS+01	;A CHEAP RANDOM NO.
013f : 18                       clc				;PREPARE
0140 : 398b00                   and MASK,y		;MASK DURATION VAL.
0143 : 798700                   adc TIME,y		;ADD MINIMUM VAL.
0146 : 290f                     and #$0F		;AND MASK RESULT
0148 : aa                       tax				;USE AS COUNTER AND
0149 : a901                     lda #$01		;DO DURATIONS AS
014b : 2a               NT2     rol a			;POWERS OF 2. CARRY
014c : ca                       dex				;SET DOTS NOTE
014d : d0fc                     bne NT2			;NOT DONE - LOOP
014f : 99c300                   sta NTBB,y		;PUT RESULT IN NOTES
0152 : 60                       rts				;TIMER AND RETURN
                        ;
                        ;ALLOCATION 0151
                        ;
                        ;SEES IF NEW NOTES ARE NEED AND IF
                        ;SO GETS THEM.  ALSO CLEARS TRIGGER
                        ;OF NOTE OUTPUT ONCE IT is PLAYED.
                        ;
0153 : a204             ALOC    ldx #$04		;DO 4 NOTE CHANNELS
0155 : d6c3             LP6     dec <NTBB,x		;DECREMENT NOTE TIMER
0157 : d007                     bne LP5			;AND IF TIME OUT
0159 : 8a                       txa				;TRANSFER X REG. TO
015a : a8                       tay				;TO Y
015b : 201601                   jsr NWNT		;AND GET NEW NOTE
015e : 98                       tya				;AND DURATION AND
015f : aa                       tax				;RESTORE X
0160 : ca               LP5     dex				;DECRIMENT COUNTER
0161 : d0f2                     bne LP6			;IF NOT DONE - LOOP
0163 : a204                     ldx #$04		;AGAIN, FOUR CHANNELS
0165 : b5bf             AL1     lda <NTB7,x		;GET NOTE FROM TEMP
0167 : 95db                     sta <NT0B,x		;BUFFER, SAVE IN OUT
0169 : 293f                     and #$3F		;BUFFER, CLEAR FLAG
016b : 95bf                     sta <NTB7,x		;PUT BACK IN TEMP.
016d : ca                       dex				;POINT TO NEXT
016e : d0f5                     bne AL1			;NOT DONE - LOOP
0170 : 60                       rts				;DONE, RETURN
                        ;
                        ;            SET
                        ;
                        ;PREPARES KNOWN START POINT FOR
                        ;CYCLIC TUNES.
                        ;
                        ;
0171 : a900             SET		lda #$00		;TO ZERO THINGS WITH
0173 : a001                     ldy #$01		;PRESET FOR NOTE CNTRS
0175 : a204                     ldx #$04		;DO 4 CHANNLES
0177 : 95db             LP10    sta <NT0B,x		;ZERO OUT-BUFFERS
0179 : 95a0                     sta <RND0,x		;ZERO 4 DICE
017b : 94c3                     sty <NTBB,x		;PRESET NOTE TIMERS
017d : 48                       pha				;SAVE THE ZERO
017e : b5cf                     lda <NTMP,x		;SET UP RNDM'S S/R
0180 : 95a4                     sta <NOIS,x		;AND CYCLE COUNTER
0182 : 68                       pla				;RECOVER ZERO
0183 : ca                       dex				;POINT TO NEXT
0184 : d0f1                     bne LP10		;NOT DONE - LOOP
0186 : 85a0                     sta <RND0		;ZERO 5TH DIE
0188 : 85ea                     sta <OUTS		;ZERO PINKING COUNTER
018a : 60                       rts				;AND RETURN
                        
                        		end
                        		

-------------------------------- Symbol Table --------------------------------

              Symbol   Value        Decimal

                ALOC : $0153            339
                 AL1 : $0165            357
                 BEG : $0000              0
                BRAK : $ffc0          65472   *
                CLCK : $00bf            191   *
                CTRL : $00e8            232
                DECD : $0f00           3840   *
                DISP : $0820           2080   *
                DURA : $013d            317
                INIT : $0d21           3361   *
                 KBD : $0810           2064   *
                KTBL : $00df            223   *
                LNTH : $00a8            168
                LOOP : $000b             11
                 LP0 : $000e             14
                 LP1 : $010c            268
                LP10 : $0177            375
                 LP3 : $001d             29
                 LP5 : $0160            352
                 LP6 : $0155            341
                MAIN : $0011             17
                MASK : $008b            139   *
               MASKA : $008f            143
               MASKB : $008e            142
               MASKC : $008d            141
               MASKD : $008c            140
                NBUF : $0090            144
                NOIS : $00a4            164
                NOTE : $0d2b           3371   *
                NTBB : $00c3            195   *
                NTB7 : $00bf            191   *
                NTMP : $00cf            207   *
                NT0B : $00db            219   *
                 NT2 : $014b            331
                NWNT : $0116            278
                 NW1 : $0122            290
                 NW2 : $0130            304
                ODLY : $00e9            233
                 OUT : $0025             37
                OUTS : $00ea            234   *
                OUTT : $00eb            235   *
                OUT1 : $0017             23
                RNDM : $0100            256
                RND0 : $00a0            160
                 SET : $0171            369
                STAR : $0003              3
                TEMP : $00ec            236   *
                TEST : $0043             67
                TIME : $0087            135   *
               TIMEA : $008b            139
               TIMEB : $008a            138
               TIMEC : $0089            137
               TIMED : $0088            136
                TMPO : $00a9            169
                TST1 : $004a             74
                TST2 : $0054             84
                TST3 : $005c             92
                TST4 : $0064            100
				