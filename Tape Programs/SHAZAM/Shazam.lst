                        ;SHAZAM 0.1
                        ;A keyboard operating system for the PAiA 8700 compute
                        ;By: Bob Yannes October 1978
                        
0f00 =                  DECODE		= $0F00 ;PIEBUG function
0d21 =                  INIT		= $0D21 ;MUS-1 function
0d2b =                  NOTE		= $0D2B ;MUS-1 function
0d52 =                  FILL		= $0D52 ;MUS-1 function
0d71 =                  POLY		= $0D71 ;MUS-1 function
0dc3 =                  TRNGN		= $0DC3 ;MUS-1 function
                        
0820 =                  DISPLAY		= $0820
                        
00ea =                  OUTS		= $EA
00e7 =                  KTBLMAX		= $E7
0097 =                  OLDKEY		= $97	;CONTAINS PREVIOUS NOTE FOR COMPATISON W
0098 =                  COUNT		= $98	;VOICE COUNTER/POINTER
0099 =                  NTS 		= $99	;START OF NTABLE FOR GIVEN NUMBER OF VOICE
                        ;NTS+1 				(=0) SPECIFIES ZERO PAGE FOR INDIRECT ADDRE
009b =                  SPLITMIN	= $9B	;DUMMY SPLIT POINT, ASSURES HIGHEST NOT
                        					;TO LAST CHANNEL.
009c =                  SPLIT		= $9C	;09C-0A2 TABLE OF SPLIT POINTS.
                        
00bf =                  CLK			= $BF	;MUS-1 clock
00df =                  KTBL		= $DF	;MUS-1
                        
                        ;Keyboard command constants
0013 =                  ENT			= $13
0014 =                  PCH			= $14
0015 =                  PCL			= $15
0016 =                  TAPE		= $16
                        ;2nd C note
005c =                  CNOTE		= $5C
                        
0100 =                  	org $0100
                        	
0100 : a2ff             		ldx #$FF		;GET THE STACK
0102 : 9a               		txs				;OUT OF THE WAY.
0103 : a9df             		lda #$DF		;GET NTABLEMAX POSITION...
0105 : 38               		sec				;PREPARE TO SUBTRACT...
0106 : e5ea             		sbc OUTS		;WELL, SUBTRACT THE NUMBER OF VOICES
0108 : 8599             		sta NTS			;THIS IT EH START OF NTABLE NOW.
010a : a900             		lda #0			;ASSURE ADDR HIGH IS ZERO (PAGE)
010c : 859a             		sta NTS+1		;FOR USE AS INDIRECT ADDR.
010e : 20210d           OPTN	jsr INIT		;WIPE'EM OUT!
0111 : 20710d           PLOOP	jsr POLY		;ASSIGN NOTES ACCORDING TO POLY.
0114 : 20c30d           		jsr TRNGN		;DO TRANSIENTS IF SELECTED.
0117 : 202b0d           		jsr NOTE		;PLAY THE NOTES.
011a : a5bf             		lda CLK			;BELLS AND
011c : 8d2008           		sta DISPLAY		;WHISTLES.
011f : 20000f           		jsr DECODE		;SCAN 8700 KYBD.
0122 : c901             		cmp #1			;IS IT TUNE?
0124 : 90e8             		bcc OPTN		;NO, IT'S LESS--MUST BE CLEAR.
0126 : d007             		bne MORE		;NO, GO SEE WHAT IT WAS.
0128 : a05c             		ldy #CNOTE		;YES, GET NOTE TO TUNE WITH...
012a : 20520d           		jsr FILL		;PUT IN ALL CHANNELS...
012d : f0e2             		beq PLOOP		;AND PLAY IT.
012f : c914             MORE	cmp #PCH		;IS IT PCH?
0131 : f006             		beq CHORUS		;YES, GO TO CHORUS,
0133 : c915             		cmp #PCL		;OKAY, THEN IS IT PCL?
0135 : f032             		beq SPLITZ		;YES, GO TO SPLITZ.
0137 : d0d8             		bne PLOOP		;NO, TWAS NONE OF THEM, KEEP ON POLY.
                        	
0139 : a5ea             CHORUS	lda OUTS		;GET THE NUMBER OF AVAILABLE VOICES.
013b : 8598             		sta COUNT		;USE AS COUNTER/POINTER
013d : 20bc01           IN		jsr CLEAR		;TURN OFF ALL GATES.
0140 : a5e7             		lda KTBLMAX		;GET LOWEST NOTE.
0142 : f00c             		beq OUT			;IF ZERO, SKIP ASSIGNMENT.
0144 : c597             		cmp OLDKEY		;IS IT THE SAME KEY YOU JUST HAD?
0146 : f004             		beq SAME		;YES, KEEP OLD CHANNEL ASSIGNMENT.
0148 : c698             		dec COUNT		;NO, GET NEW CHANNEL ASSIGNMENT.
014a : f004             		beq OUT			;IF ZERO, SKIP ASSIGNMENT.
014c : a498             SAME	ldy COUNT		;GET POINT.
014e : 9199             		sta (NTS),y		;ASSIGN VOICE TO CHANNEL Y.
0150 : 8597             OUT		sta OLDKEY		;ALSO, STOE IT FOR LATER.
0152 : 202b0d           		jsr NOTE		;PLAY THE NOTE.
0155 : 20000f           		jsr DECODE		;SCAN 8700 KYBD.
0158 : c915             		cmp #PCL		;IS IT PCL?
015a : f00d             		beq SPLITZ		;YES, GO TO SPLITZ.
015c : c916             		cmp #TAPE		;IS IT TAPE?
015e : f0b1             		beq PLOOP		;YES, GO TO POLY.
0160 : a598             		lda COUNT		;GET COUNT.
0162 : f0d5             		beq CHORUS		;IF ZERO, START OVER.
0164 : 8d2008           		sta DISPLAY		;IF NOT, GIVE US A LOOK...
0167 : d0d4             		bne IN			;AND KEEP ON CHORUS.
0169 : a5ea             SPLITZ	lda OUTS		;GET THE NUMBER OF AVAILABLE VOICES.
016b : 8597             		sta OLDKEY		;USE AS COUNTER/POINTER.
016d : 8597             SELECT	sta OLDKEY		;STORE THE PRESENT NOTE FOR LATER.
016f : 202b0d           		jsr NOTE		;REFRESH QUASH AND LOAD KTABLE.
0172 : a5e7             		lda KTBLMAX		;GET LOWEST NOTE.
0174 : f0f7             		beq SELECT		;IF ZERO, TRY AGAIN.
0176 : a497             		ldy OLDKEY		;GET LAST NOTE.
0178 : d0f3             		bne SELECT		;IF NOT ZERO, YOU'RE STILL HOLDING IT.
017a : a498             		ldy COUNT		;IT'S A NEW NOTE--GET COUNTER/POINTER
017c : 8c2008           		sty DISPLAY		;SHOW THE COUNT.
017f : 999c00           		sta SPLIT,y		;STORE THE NOTE AS SPLIT POINT Y.
0182 : c698             		dec COUNT		;NEXT SPLIT POINT.
0184 : d0e7             		bne SELECT		;GO BACK UNTIL ALL SPLIT POINTS ARE IN.
0186 : a9ff             		lda #$FF		;DUMMY SPLIT POINT...
0188 : 859b             		sta SPLITMIN	;STORED AT END OF TABLE.
018a : 20bc01           PERF	jsr CLEAR		;ALL SPLITS ARE IN, TURN OFF ALL GATES
018d : a208             		ldx #8			;PREPARE TO TEST KTABLE.
018f : b5df             NXTNOT	lda KTBL,x		;GET NOTE X FROM IT.
0191 : f00c             		beq NXT2		;IF ZERO, DO NEXT.
0193 : a4ea             		ldy OUTS		;GET COUNTER/POINTER.
0195 : d99c00           NXT0	cmp SPLIT,y		;COMPARE NOTE TO SPLIT POINT Y.
0198 : 9003             		bcc NXT1		;IF LESS THAN, GO ASSIGN IT.
019a : 88               		dey				;NEXT SPLIT POINT.
019b : d0f8             		bne NXT0		;KEEP ON UNTIL YOU'VE CHECKED ALL.
019d : 9199             NXT1	sta (NTS),y		;ASSIGN NOTE TO CHANNEL Y.
019f : ca               NXT2	dex				;NEXT KEYBOARD ENTRY.
01a0 : d0ed             		bne NXTNOT		;KEEP ON UNTIL ALL KEYBOARD DONE.
01a2 : 202b0d           		jsr NOTE		;PLAY THE NOTES.
01a5 : a5bf             		lda CLK			;MORE BELLS AND
01a7 : 8d2008           		sta DISPLAY		;WHISTLES.
01aa : 20000f           		jsr DECODE		;SCAN 8700 KYBD.
01ad : c914             		cmp #PCH		;IS IT PCH?
01af : f088             		beq CHORUS		;YES, GO TO CHORUS.
01b1 : c913             		cmp #ENT		;IS IT ENT?
01b3 : f0b4             		beq SPLITZ		;YES, GO GET NEW SPLIT POINTS.
01b5 : c916             		cmp #TAPE		;IS IT TAPE?
01b7 : d0d1             		bne PERF		;NO, KEEP ON PLAYING SPLITZ.
01b9 : 4c1101           		jmp PLOOP		;YES, GO TO POLY.
                        
01bc : a4ea             CLEAR	ldy OUTS		;GET POINTER/COUNTER.
01be : b199             NANO	lda (NTS),y		;GET NOTE BEING PLAYED ON CHANNEL Y.
01c0 : 293f             		and #$3F		;TURN OFF ITS GATE...
01c2 : 9199             		sta (NTS),y		;AND PUT IT BACK.
01c4 : 88               		dey				;NEXT CHANNEL.
01c5 : d0f7             		bne NANO		;GO BACK UNTIL DONE.
01c7 : 60               		rts				;RETURN.
                        	

-------------------------------- Symbol Table --------------------------------

              Symbol   Value        Decimal

              CHORUS : $0139            313
               CLEAR : $01bc            444
                 CLK : $00bf            191   *
               CNOTE : $005c             92   *
               COUNT : $0098            152   *
              DECODE : $0f00           3840   *
             DISPLAY : $0820           2080   *
                 ENT : $0013             19   *
                FILL : $0d52           3410   *
                  IN : $013d            317
                INIT : $0d21           3361   *
                KTBL : $00df            223   *
             KTBLMAX : $00e7            231   *
                MORE : $012f            303
                NANO : $01be            446
                NOTE : $0d2b           3371   *
                 NTS : $0099            153   *
              NXTNOT : $018f            399
                NXT0 : $0195            405
                NXT1 : $019d            413
                NXT2 : $019f            415
              OLDKEY : $0097            151   *
                OPTN : $010e            270
                 OUT : $0150            336
                OUTS : $00ea            234   *
                 PCH : $0014             20   *
                 PCL : $0015             21   *
                PERF : $018a            394
               PLOOP : $0111            273
                POLY : $0d71           3441   *
                SAME : $014c            332
              SELECT : $016d            365
               SPLIT : $009c            156   *
            SPLITMIN : $009b            155   *
              SPLITZ : $0169            361
                TAPE : $0016             22   *
               TRNGN : $0dc3           3523   *
			   