                        ;
                        ;
                        ;*********************************
                        ;*                               *
                        ;*          DRUMSYS 0.6          *
                        ;*                               *
                        ;*   8700/EK-2 DRUM OPERATING    *
                        ;*            SYSTEM             *
                        ;*              BY               *
                        ;*         JOHN SIMONTON         *
                        ;*                               *
                        ;*(C) 1978 - PAIA ELECTRONICS,INC*
                        ;*                               *
                        ;*********************************
                        ;
00f0 =                  BUFF	= $00F0
00ec =                  CNTR	= $00EC
00eb =                  EXP		= $00EB
00ea =                  TMPO	= $00EA
00e9 =                  PNTR	= $00E9
0820 =                  DSP		= $0820
ff00 =                  DECD	= $FF00
0f22 =                  BEEP	= $0F22
0100 =                  SCOR	= $0100
0086 =                  DUMY	= $0086
0840 =                  OUTP	= $0840
0e25 =                  SNBT	= $0E25
0eaa =                  CASS	= $0EAA
                        ;---------------------
                        ;00E8
                        ;    S-TABLE (CONTROL CODES)
                        ;00E1
                        ;---------------------
00d1 =                  STBL	= $00D1
                        ;---------------------
                        ;00E0
                        ;    DRUM SIGNATURES
                        ;00D9
                        ;---------------------
                        ;*********************************
10d2 =                  		org $10D2 ;org $10D1
                        ;*********************************
                        ;
10d2 : ff00800100010001 TAPE	db	$FF, $00, $80, $01, $00, $01, $00, $01
10da : fffefdf3f7efdfbf DSIG	db	$FF, $FE, $FD, $F3, $F7, $EF, $DF, $BF
10e2 : 6a9d8d877cb2b9.. CRL		db	$6A, $9D, $8D, $87, $7C, $B2, $B9, $CD, $00, $
                        ;
10ed =                  		org $10ED
10ed : ff               PARM	db	$FF
10f6 =                  		org $10F6
10f6 : f6f7f8f9fafbfc.. PAR1	db	$F6, $F7, $F8, $F9, $FA, $FB, $FC, $FD, $FF, $
                        ;*********************************
1000 =                  		org $1000
                        ;*********************************
                        ;
1000 : a986             SPHK	lda	#$86		;SPARE HOOK KEYS 8-F3
1002 : 8539             		sta	<lo ACTN+01	;USED ONLY TO RE-START
1004 : ea               		nop				;SYTEM.  IN LATER VERSIONS
1005 : ea               		nop				;WILL PROVIDE ADDITTIONAL
                        ;						 FEATURES
                        ;
1006 : a900             STAR	lda	#$00		;PREPARE ACCUMULATOR AND
1008 : 85e9             		sta	<lo PNTR	;ZERO SCORE POINTER
100a : 8d2008            		sta	DSP			;AND DISPLAYS
100d : aa               		tax				;PREPARE X REG AS POINTER
100e : 9d0001           SLP0	sta	SCOR,x		;AND USE IT TO CLEAR SCORE
1011 : e8               		inx
1012 : d0fa             		bne	SLP0		;LOOP UNTIL DONE
1014 : 205310           SLP1	jsr	RDKY		;GO READ THE KEYBOARD, ETC.
1017 : b01f             		bcs	ACTN		;AND IF NO NEW KEYS, BRAND
1019 : c910             		cmp	#$10		;NEW KEY - A "CONTROL" KEY?
101b : b016             		bcs	CTRL		;YES - BRANCH TO CONTROL
101d : c908             		cmp	#$08		;ONE OF "SPARE" KEYS?
101f : b0df             		bcs	SPHK		;YES- BRANCH
                        ; 		**
1021 : a986             NTRY	lda	#$86		;DRUM ENTRY MODE, GET LINK
1023 : 8539             		sta	<lo ACTN+01	;SET LINK
1025 : b9da10            		lda	DSIG,y		;GET DRUM SIGNATURE
1028 : a6e9             		ldx	<lo PNTR	;GET SCORE POINTER
102a : 9d0001            		sta	SCOR,x		;SAVE DRUM SIG IN SCORE
102d : 203e10            		jsr	PLAY		;PLAY THE DRUM BEAT
1030 : 4c1410            		jmp	SLP1		;LOOP FOR MORE
1033 : b9d100           CTRL	lda	STBL,y		;GET COMMAND ADDRESS LINK
1036 : 8539             		sta	<lo ACTN+01;AND SET LINK IN JSR DUMY
1038 : 208600           ACTN	jsr	DUMY		;AND GO TO COMMAND SUBROUTINE
103b : 4c1410            		jmp	SLP1		;THEN LOOP FOR MORE
                        ;
                        ;PLAY SUBROUTINE
                        ;
103e : a4eb             PLAY	ldy	<lo EXP		;GET EXPRESSION VARIABLE
1040 : 8d4008           		sta	OUTP		;OUTPUT CONTROL TO EK-2
1043 : 297f             		and	#$7F		;RESET STROBE BIT
1045 : 88               PLA0	dey				;DELAY FOR THE EXP. TIME
1046 : d0fd             		bne	PLA0		;LOOP UNTIL DONE
1048 : 8d4008            		sta	OUTP		;AND TURN DRUM "OFF"
104b : e6e9             		inc	<lo PNTR	;INCREMENT SCORE POINTER
104d : a6e9             		ldx	<lo PNTR	;PLACE IN X REGISTER
104f : 8e2008            		stx	DSP			;AND SHOW IN DISPLAYS
1052 : 60               		rts				;THEN RETURN
                        ;
                        ;READ KEY-ALSO IMPORTANT TO TEMPO
                        ;
1053 : 2000ff           RDKY	jsr	DECD		;PIEBUG KEYBOARD SUBROUTINE
1056 : b005             		bcs	DLY			;SAME KEY - JUST DELAY
1058 : a200             		ldx	#$00
105a : 86ec             		stx	<lo CNTR	;ZERO TEMPO COUNTER
105c : 60               		rts
105d : a220             DLY		ldx	#$20		;SET X AN Y REGISTER
105f : a03f             NXTX	ldy	#$3F		;DELAY PARAMETERS
1061 : 88               DELY	dey				;AND DO DELAY
1062 : d0fd             		bne	DELY
1064 : ca               		dex
1065 : d0f8             		bne	NXTX		;LOOP UNTIL DONE
1067 : e6ec             		inc	<lo CNTR	;INCREMENT TEMPO COUNTER
1069 : 60               		rts				;AND RETURN
                        ;
                        ;RUN SUBROUTINE
                        ;
                        ;		**
106a : a97c             RUN		lda	#lo WAIT	;COMMAND LINK TO "WAIT"
106c : 8539             		sta	<lo ACTN+01	;SET COMMAND LINK
106e : a900             CYCL	lda	#$00		;PREPARE AND SET
1070 : 85e9             		sta	<lo PNTR	;SCORE POINTER TO 0
1072 : a6e9             CONT	ldx	<lo PNTR	;GET CURRENT SCORE POINTER
1074 : bd0001            		lda	SCOR,x		;GET CURRENT DRUM SIGNATURE
1077 : f0f5             		beq	CYCL		;ZERO, END OF SCORE-BRANCH
1079 : 203e10           		jsr	PLAY		;GO PLAY DRUM SOUND, ETC.
107c : a5ec             WAIT	lda	<lo CNTR	;GET TEMPO COUNTER AND
107e : 45ea             		eor	<lo TMPO	;COMPARE TO TEMPO VARIABLE
1080 : d004             		bne	RETN		;IF NOT TIMED OUT, RETURN
1082 : 85ec             OCNT	sta	<lo CNTR	;TIMED OUT - ZERO COUNTER
1084 : f0ec             		beq	CONT		;BRANCH ALWAYS TO PLAY, ETC
1086 : 60               RETN	rts				;RETURN
                        ;
                        ;SINGLE STEP SUBROUTINE
                        ;
                        ;		**
1087 : a986             STEP	lda	#lo RETN	;COMMAND LINK TO "RETN"
1089 : 8539             		sta	<lo ACTN+01	;SET COMMAND LINK
108b : d0e5             		bne	CONT		;BRANCH ALWAYS TO PLAY, ETC.
                        ;
                        ;BACKSPACE SUBROUTINE
                        ;
                        ;		**
108d : a996             BACK	lda	#lo NEXT	;COMMAND LINK TO "NEXT"
108f : 8539             		sta	<lo ACTN+01	;SET COMMAND LINK
1091 : c6e9             		dec	<lo PNTR	;SCORE POINTER BACK ONE
1093 : d0dd             		bne	CONT		;GO PLAY SCORE, ETC.
1095 : 60               		rts				;AND RETURN
                        ;		**
1096 : a986             NEXT	lda	#lo RETN	;COMMAND LINK TO "RETN"
1098 : 8539             		sta	<lo ACTN+01	;SET COMMAND LINK
109a : c6e9             		dec	<lo PNTR	;SCORE POINTER BACK ONE
109c : 60               		rts				;RETURN
                        ;
                        ;TEMPO
                        ;
                        ;		**
109d : a9a5             TMP		lda	#lo NXT2	;COMMAND LINK TO "NXT2"
109f : 8539             		sta	<lo ACTN+01	;SET COMMAND LINK
10a1 : a900             		lda	#$00		;INITIALIZE TEMPO COUNTER
10a3 : 85ea             		sta	<lo TMPO	;AND START COUNTIN
10a5 : e6ea             NXT2	inc <lo TMPO	;UNTIL NEXT COMMAND
10a7 : 60               		rts				;RETURN
                        ;
                        ;SET UP FOR TAPE TRANSFER
                        ;
10a8 : a207             STTP	ldx	#$07		;TRANSFER SEVEN BYTES
10aa : b5d2             STP		lda	<lo TAPE,x 	;GET PARAMETER
10ac : 95f0             		sta	BUFF,x		;PLACE PARAMETER
10ae : ca               		dex				;POINT TO NEXT
10af : d0f9             		bne	STP			;LOOP UNTIL ALL TRANSFERED
10b1 : 60               		rts				;THEN RETURN
                        ;
                        ;TAPE IN AND OUT ROUTINES
                        ;
10b2 : 20a810           TOUT	jsr	STTP		;SET UP PARAMETERS
10b5 : a9dd             		lda	#$DD		;SET DUMP "SWITCH"
10b7 : d005             		bne	DO			;BRANCH ALWAYS
10b9 : 20a810           TIN		jsr	STTP		;SET UP PARAMETERS
10bc : a911             		lda	#$11		;SET LOAD "SWITCH"
10be : 20250e           DO		jsr	SNBT		;TURN ON RELAYS
10c1 : 20aa0e            		jsr	CASS		;DO CASSETTE ROUTINE
                        ;		**
10c4 : a986             		lda	#lo RETN	;COMMAND LINK TO "RETN"
10c6 : 8539             		sta	<lo ACTN+01	;SET LINK
10c8 : 18               		clc				;PREPARE FOR BEEP
10c9 : 20220f            		jsr	BEEP		;TURN OFF RELAYS AND BEEP
10cc : 60               		rts				;AND RETURN
                        ;
                        ;STROBE DRUM EFFECT
                        ;
10cd : c6e9             STRB	dec	<lo PNTR	;PREPARE TO GET SAME DRUM
10cf : 4c7210            		jmp	CONT		;PLAY DRUM
                        ;
                        ;
                        ;
                        ;
                        	end

-------------------------------- Symbol Table --------------------------------

              Symbol   Value        Decimal

                ACTN : $1038           4152
                BACK : $108d           4237
                BEEP : $0f22           3874   *
                BUFF : $00f0            240   *
                CASS : $0eaa           3754   *
                CNTR : $00ec            236   *
                CONT : $1072           4210
                 CRL : $10e2           4322
                CTRL : $1033           4147
                CYCL : $106e           4206
                DECD : $ff00          65280   *
                DELY : $1061           4193
                 DLY : $105d           4189
                  DO : $10be           4286
                DSIG : $10da           4314
                 DSP : $0820           2080   *
                DUMY : $0086            134   *
                 EXP : $00eb            235   *
                NEXT : $1096           4246
                NTRY : $1021           4129
                NXTX : $105f           4191
                NXT2 : $10a5           4261
                OCNT : $1082           4226
                OUTP : $0840           2112   *
                PARM : $10ed           4333
                PAR1 : $10f6           4342
                PLAY : $103e           4158
                PLA0 : $1045           4165
                PNTR : $00e9            233   *
                RDKY : $1053           4179
                RETN : $1086           4230
                 RUN : $106a           4202
                SCOR : $0100            256   *
                SLP0 : $100e           4110
                SLP1 : $1014           4116
                SNBT : $0e25           3621   *
                SPHK : $1000           4096
                STAR : $1006           4102
                STBL : $00d1            209   *
                STEP : $1087           4231
                 STP : $10aa           4266
                STRB : $10cd           4301
                STTP : $10a8           4264
                TAPE : $10d2           4306
                 TIN : $10b9           4281
                 TMP : $109d           4253
                TMPO : $00ea            234   *
                TOUT : $10b2           4274
                WAIT : $107c           4220